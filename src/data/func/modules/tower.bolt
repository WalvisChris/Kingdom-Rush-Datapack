import math

function func:tower/clear:
    fill ~-2 ~-2 ~-2 ~2 ~3 ~2 air

function func:tower/archer1:
    clone 6 0 33 2 3 29 ~-2 ~-2 ~-2

function func:tower/archer2:
    clone 6 0 27 2 3 23 ~-2 ~-2 ~-2

function func:tower/archer3:
    clone 6 0 21 2 3 17 ~-2 ~-2 ~-2

function func:tower/knight1:
    clone 12 0 33 8 3 29 ~-2 ~-2 ~-2

function func:tower/knight2:
    clone 12 0 27 8 3 23 ~-2 ~-2 ~-2

function func:tower/knight3:
    clone 12 0 21 8 3 17 ~-2 ~-2 ~-2

function func:tower/wizzard1:
    clone 18 0 33 14 3 29 ~-2 ~-2 ~-2

function func:tower/wizzard2:
    clone 18 0 27 14 3 23 ~-2 ~-2 ~-2

function func:tower/wizzard3:
    clone 18 0 21 14 3 17 ~-2 ~-2 ~-2

function func:tower/bomb1:
    clone 24 0 33 20 3 29 ~-2 ~-2 ~-2

function func:tower/bomb2:
    clone 24 0 27 20 3 23 ~-2 ~-2 ~-2

function func:tower/bomb3:
    clone 24 0 21 20 3 17 ~-2 ~-2 ~-2

function func:tower/upgrade_all:
    say checking archer1
    execute if entity @s[tag=tower.archer1,tag=!upgraded]:
        execute at @s run function func:tower/archer2
        tag @s add tower.archer2
        tag @s add upgraded
        tag @s remove tower.archer1
    say checking archer2
    execute if entity @s[tag=tower.archer2,tag=!upgraded]:
        execute at @s run function func:tower/archer3
        tag @s add tower.archer3
        tag @s add upgraded
        tag @s remove tower.archer2
    say checking knight1
    execute if entity @s[tag=tower.knight1,tag=!upgraded]:
        execute at @s run function func:tower/knight2
        kill @e[type=item]
        tag @s add tower.knight2
        tag @s add upgraded
        tag @s remove tower.knight1

        say upgrading armor1
        execute if entity @s[tag=soldiers.right] at @s positioned ~5 ~-2 ~ run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor2
        execute if entity @s[tag=soldiers.up] at @s positioned ~ ~-2 ~-5 run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor2
        execute if entity @s[tag=soldiers.down] at @s positioned ~ ~-2 ~5 run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor2
        execute if entity @s[tag=soldiers.left] at @s positioned ~-5 ~-2 ~ run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor2
        
    say checking knight2
    execute if entity @s[tag=tower.knight2,tag=!upgraded]:
        execute at @s run function func:tower/knight3
        kill @e[type=item]
        tag @s add tower.knight3
        tag @s add upgraded
        tag @s remove tower.knight2

        say upgrading armor2
        execute if entity @s[tag=soldiers.right] at @s positioned ~5 ~-2 ~ run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor3
        execute if entity @s[tag=soldiers.up] at @s positioned ~ ~-2 ~-5 run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor3
        execute if entity @s[tag=soldiers.down] at @s positioned ~ ~-2 ~5 run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor3
        execute if entity @s[tag=soldiers.left] at @s positioned ~-5 ~-2 ~ run execute as @e[tag=soldier,limit=3,sort=nearest,distance=..2] run function func:soldier/armor3
    
    say checking wizzard1
    execute if entity @s[tag=tower.wizzard1,tag=!upgraded]:
        execute at @s run function func:tower/wizzard2
        tag @s add tower.wizzard2
        tag @s add upgraded
        tag @s remove tower.wizzard1
    say checking wizzard2
    execute if entity @s[tag=tower.wizzard2,tag=!upgraded]:
        execute at @s run function func:tower/wizzard3
        tag @s add tower.wizzard3
        tag @s add upgraded
        tag @s remove tower.wizzard2
    say checking bomb1
    execute if entity @s[tag=tower.bomb1,tag=!upgraded]:
        execute at @s run function func:tower/bomb2
        tag @s add tower.bomb2
        tag @s add upgraded
        tag @s remove tower.bomb1
    say checking bomb2
    execute if entity @s[tag=tower.bomb2,tag=!upgraded]:
        execute at @s run function func:tower/bomb3
        tag @s add tower.bomb3
        tag @s add upgraded
        tag @s remove tower.bomb2
    execute if entity @s[tag=upgraded] at @p run playsound minecraft:block.enchantment_table.use master @p ~ ~ ~ 1 1.5
    execute if entity @s[tag=upgraded] at @p run playsound block.anvil.use master @p ~ ~ ~ 1 1.7
    tag @s[tag=upgraded] remove upgraded

function func:tower/knight/soldiers:
    summon armor_stand ~1 ~ ~-1 {Tags:["soldier"],ArmorItems:[{id:"leather_boots",Count:1},{id:"leather_leggings",Count:1},{id:"leather_chestplate",Count:1},{id:"minecraft:player_head",count:1,components:{"minecraft:custom_name":'{"text":"Knight","color":"gold","underlined":true,"bold":true,"italic":false}',"minecraft:lore":['{"text":"Custom Head ID: 117136","color":"gray","italic":false}','{"text":"www.minecraft-heads.com","color":"blue","italic":false}'],"minecraft:profile":{id:[I;828922402,-1626127593,-1586408291,1718285182],properties:[{name:"textures",value:"eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNzJmNTYwYzZlYmYxMDY2OGMyYzY0OWQ2YjJiNDI5MzVhOGE2NjBmMjlmZWYwNTZjNTE4Mzk3ODQ2ZDdlMTRmIn19fQ=="}]}}}],HandItems:[{},{id:"wooden_sword",Count:1}],Pose:{RightArm:[350.0f,0.0f,10.0f],LeftArm:[350.0f,0.0f,350.0f]},ShowArms:1,NoBasePlate:1,CustomName:'{"text":"soldier.a"}'}
    summon armor_stand ~ ~ ~1 {Tags:["soldier"],ArmorItems:[{id:"leather_boots",Count:1},{id:"leather_leggings",Count:1},{id:"leather_chestplate",Count:1},{id:"minecraft:player_head",count:1,components:{"minecraft:custom_name":'{"text":"Knight","color":"gold","underlined":true,"bold":true,"italic":false}',"minecraft:lore":['{"text":"Custom Head ID: 117136","color":"gray","italic":false}','{"text":"www.minecraft-heads.com","color":"blue","italic":false}'],"minecraft:profile":{id:[I;828922402,-1626127593,-1586408291,1718285182],properties:[{name:"textures",value:"eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNzJmNTYwYzZlYmYxMDY2OGMyYzY0OWQ2YjJiNDI5MzVhOGE2NjBmMjlmZWYwNTZjNTE4Mzk3ODQ2ZDdlMTRmIn19fQ=="}]}}}],HandItems:[{},{id:"wooden_sword",Count:1}],Pose:{RightArm:[350.0f,0.0f,10.0f],LeftArm:[350.0f,0.0f,350.0f]},ShowArms:1,NoBasePlate:1,CustomName:'{"text":"soldier.b"}'}
    summon armor_stand ~-1 ~ ~-1 {Tags:["soldier"],ArmorItems:[{id:"leather_boots",Count:1},{id:"leather_leggings",Count:1},{id:"leather_chestplate",Count:1},{id:"minecraft:player_head",count:1,components:{"minecraft:custom_name":'{"text":"Knight","color":"gold","underlined":true,"bold":true,"italic":false}',"minecraft:lore":['{"text":"Custom Head ID: 117136","color":"gray","italic":false}','{"text":"www.minecraft-heads.com","color":"blue","italic":false}'],"minecraft:profile":{id:[I;828922402,-1626127593,-1586408291,1718285182],properties:[{name:"textures",value:"eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNzJmNTYwYzZlYmYxMDY2OGMyYzY0OWQ2YjJiNDI5MzVhOGE2NjBmMjlmZWYwNTZjNTE4Mzk3ODQ2ZDdlMTRmIn19fQ=="}]}}}],HandItems:[{},{id:"wooden_sword",Count:1}],Pose:{RightArm:[350.0f,0.0f,10.0f],LeftArm:[350.0f,0.0f,350.0f]},ShowArms:1,NoBasePlate:1,CustomName:'{"text":"soldier.c"}'}
    execute at @p run playsound minecraft:item.armor.equip_chain master @p ~ ~ ~ 1 1

function func:tower/update:
    # archer
    execute if entity @s[tag=tower.archer]:
        execute at @s anchored eyes if entity @e[tag=enemy,limit=1,sort=nearest,distance=..8]:
            
            tp @s ~ ~ ~ facing entity @e[tag=enemy,limit=1,sort=nearest,distance=..8] feet
            execute unless score @s timer matches 20.. run scoreboard players add @s timer 1

            # shoot
            execute if score @s timer matches 20..:
                function func:debug/shoot/archer
                scoreboard players remove @e[tag=enemy,limit=1,sort=nearest,distance=..8] health 1
                execute at @p run playsound minecraft:entity.arrow.shoot master @p ~ ~ ~ 1 1.5
                scoreboard players set @s timer 0
        
        execute at @s unless entity @e[tag=enemy,limit=1,sort=nearest,distance=..8] run scoreboard players reset @s timer
        function func:debug/bar/hiderate

    # knight
    execute if entity @s[tag=tower.knight]:
        
        execute at @s if block ~5 ~-3 ~ beehive unless entity @s[tag=full]:
            execute at @s positioned ~5 ~-2 ~ run function func:tower/knight/soldiers
            tag @s add soldiers.right
            tag @s add full
        execute at @s if block ~ ~-3 ~-5 beehive unless entity @s[tag=full]:
            execute at @s positioned ~ ~-2 ~-5 run function func:tower/knight/soldiers
            tag @s add soldiers.up
            tag @s add full
        execute at @s if block ~ ~-3 ~5 beehive unless entity @s[tag=full]:
            execute at @s positioned ~ ~-2 ~5 run function func:tower/knight/soldiers
            tag @s add soldiers.down
            tag @s add full
        execute at @s if block ~-5 ~-3 ~ beehive unless entity @s[tag=full]:
            execute at @s positioned ~-5 ~-2 ~ run function func:tower/knight/soldiers
            tag @s add soldiers.left
            tag @s add full

    # wizzard
    execute if entity @s[tag=tower.wizzard]:
        execute at @s anchored eyes if entity @e[tag=enemy,limit=1,sort=nearest,distance=..8]:

            tp @s ~ ~ ~ facing entity @e[tag=enemy,limit=1,sort=nearest,distance=..8] feet
            execute unless score @s timer matches 20.. run scoreboard players add @s timer 1

            # shoot
            execute if score @s timer matches 20..:
                function func:debug/shoot/wizzard
                scoreboard players remove @e[tag=enemy,limit=1,sort=nearest,distance=..8] health 2
                execute at @p run playsound minecraft:entity.illusioner.cast_spell master @p ~ ~ ~ 1 2
                scoreboard players set @s timer 0
            
        execute at @s unless entity @e[tag=enemy,limit=1,sort=nearest,distance=..8] run scoreboard players reset @s timer
        function func:debug/bar/hiderate

    # bomb
    execute if entity @s[tag=tower.bomb]:
        execute as @s at @s anchored feet if entity @e[tag=enemy,limit=1,sort=nearest,distance=..8]:
            
            tp @s ~ ~ ~ facing entity @e[tag=enemy,limit=1,sort=nearest,distance=..8] eyes
            execute unless score @s timer matches 60.. run scoreboard players add @s timer 1
            
            # shoot
            execute if score @s timer matches 60..:
                
                tag @e[tag=enemy,limit=1,sort=nearest,distance=..8] add bomb.target
                summon armor_stand ~ ~ ~ {Invisible:1,NoGravity:1,Tags:["bomb.placeholder"]}
                execute at @s rotated as @s run teleport @e[tag=bomb.placeholder,limit=1,sort=nearest,distance=..1] ~ ~ ~ ~ ~
                execute at @s run scoreboard players set @e[tag=bomb.placeholder,limit=1,sort=nearest,distance=..1] animate 0
                scoreboard players set @s timer 0
        
        execute at @s unless entity @e[tag=enemy,limit=1,sort=nearest,distance=..8] run scoreboard players reset @s timer
        function func:debug/bar/hiderate
    
    # debug
    execute if score @s timer matches 0.. run function func:debug/bar/showrate

# bomb shooting
function func:tower/bomb/placeholder:
    execute if score @s animate matches 0..:
        for frame in range(24):
            if score @s animate matches frame:
                z = frame/3
                y = math.sin(z * (math.pi / 8)) * 8
                execute at @s run particle cloud ^ ^y ^z 0 0 0 0 1
        execute if score @s animate matches 24:
            
            execute at @e[tag=bomb.target,limit=1,sort=nearest] run scoreboard players remove @e[tag=enemy,distance=..3] health 5
            execute at @e[tag=bomb.target,limit=1,sort=nearest] run particle minecraft:gust_emitter_small ~ ~ ~ 0 0 0 0 1
            tag @e[tag=bomb.target,limit=1,sort=nearest] remove bomb.target
            execute at @p run playsound entity.breeze.death master @p ~ ~ ~ 1 .1
            kill @s
        scoreboard players reset @s[scores={animate=24..}] animate
        scoreboard players add @s[scores={animate=0..24}] animate 1

    execute if score @s timer matches ..24 run scoreboard players add @s timer 1
